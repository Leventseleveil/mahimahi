#!/usr/bin/perl

# converts a trace FROM varying rate (in kilobits per millisecond, // 从可变速率转成trace文件（以千比特/毫秒为单位，每毫秒采样一次）
# sampled once per millisecond)
# TO packet delivery occurrences (millisecond timestamps when an // 数据包交付事件（MTU大小的数据报，即12000位，可交付时的毫秒时间戳）
# MTU-sized datagram, i.e. 12,000 bits, can be delivered)
#
# The latter is the format that mm-link expects. // 后者是mm-link所期望的格式。（即traces文件
#
# KJW 7/28/2017

use strict;
use warnings;
use Scalar::Util qw[looks_like_number];
use POSIX qw[ceil];

my $time = 0;
my $reserve_bits = 0;
my $PACKET_LENGTH = 12000; # bits

# input trace gives a number of kilobits that can be delivered each millisecond
while (my $kilobits_this_millisecond = <>) {
  chomp $kilobits_this_millisecond;
  die qq{Not a number: "$kilobits_this_millisecond"}
    unless looks_like_number $kilobits_this_millisecond;

  # add current rate to "reserve"
  $reserve_bits += 1000 * $kilobits_this_millisecond;

  # if reserve has enough bits for a packet, deliver it now
  # output trace gives timestamps of packet delivery occurrences
  while ( $reserve_bits >= $PACKET_LENGTH ) {
    print qq{$time\n};
    $reserve_bits -= $PACKET_LENGTH;
  }

  # advance time by one millisecond
  $time++;
}
